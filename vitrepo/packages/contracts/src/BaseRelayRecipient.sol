// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

/**
 * @dev A base contract to be inherited by any contract that wants to receive relayed transactions.
 * See EIP-2771.
 */
abstract contract BaseRelayRecipient {

    address private _trustedForwarder;

    function isTrustedForwarder(address forwarder) public view virtual returns (bool) {
        return forwarder == _trustedForwarder;
    }

    function _setTrustedForwarder(address forwarder) internal {
        _trustedForwarder = forwarder;
    }

    function _msgSender() internal view virtual returns (address ret) {
        if (msg.data.length >= 20 && isTrustedForwarder(msg.sender)) {
            // At this point we know that the sender is a trusted forwarder,
            // so we trust that the last 20 bytes of msg.data is the actual sender.
            // (See EIP-2771)
            assembly {
                ret := shr(96, calldataload(sub(calldatasize(), 20)))
            }
        } else {
            ret = msg.sender;
        }
    }

    function _msgData() internal view virtual returns (bytes calldata ret) {
        if (msg.data.length >= 20 && isTrustedForwarder(msg.sender)) {
            return msg.data[0:msg.data.length - 20];
        } else {
            return msg.data;
        }
    }
}
